{
  # Photo garden packages
  workspace, packages,

  # Dependencies
  lib,
  dockerTools,
  linkFarm, runCommand, writeText,
  bashInteractive, coreutils, less, nodejs, remarshal,
}:
let
  relativizePath = base: path: lib.removePrefix (toString base + "/") (toString path);

  # Adapted from https://github.com/xtruder/kubenix/blob/bc37b314ee5123da9f61721e2845291a2fdd0e58/k8s.nix
  loadYAML = path: builtins.fromJSON (builtins.readFile (runCommand "yaml-to-json" {} "${remarshal}/bin/remarshal -i ${path} -if yaml -of json > $out"));
in rec {
  imageConfig = linkFarm "config" [ {
    name = "photo-garden.json";
    path = ./config.development.json;
  } ];
  baseImage = dockerTools.buildImage {
    name = "photo-garden-base";
    contents = [
      # Debugging
      bashInteractive
      coreutils
      less
      # /usr/bin/env
      (linkFarm "photo-garden-base-overlay" [{
        name = "usr";
        path = linkFarm "photo-garden-base-usr" [{
          name = "bin";
          path = linkFarm "photo-garden-base-usr-bin" [{
            name = "env";
            path = "${coreutils}/bin/env";
          }];
        }];
      }])
      # Shared packages to reduce duplication
      nodejs
    ];
  };
  images = map (name: {
    name = "${name}.docker.tar.gz";
    path = dockerTools.buildImage {
      name = "photo-garden-${name}";
      tag = "latest";
      fromImage = baseImage;
      contents = [ imageConfig workspace."${name}" ];
      config = {
        Cmd = [ "/bin/${name}" ];
        Env = [ "PHOTO_GARDEN_CONFIG=/photo-garden.json"];
      };
    };
  }) packages ++ [{
    name = "docker-base.tar.gz";
    path = baseImage;
  }];

  composeFileBase = loadYAML ./docker-compose.template.yml;
  composeFileOverrides = {
    services = lib.listToAttrs (map (name: rec {
      inherit name;
      pkg = workspace.${name};
      value = {
        image = "photo-garden-${name}:latest";
        volumes =
        let
          existing = ((composeFileBase.services or {}).${name} or {}).volumes or [];
          dependencyVolumes = map (dep: "./${relativizePath ./. dep.src}:/node_modules/${dep.pname}") ([pkg] ++ lib.attrValues pkg.workspaceDependencies);
        in existing ++ dependencyVolumes;
      };
    }) packages);
  };
  composeFileData = lib.recursiveUpdate composeFileBase composeFileOverrides;
  composeFile = writeText "docker-compose.yml"
    ''
      # DO NOT EDIT DIRECTLY, THIS WAS GENERATED BY composeFile in docker.nix
      # Instead, edit docker-compose.template.yml, and run ./build-docker.sh to regenerate
      ${builtins.toJSON composeFileData}
    '';

  extraFiles = [
    {
      name = "docker-compose.yml";
      path = composeFile;
    }
  ];
}
